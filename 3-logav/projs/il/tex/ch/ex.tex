% ! TEX root = ../tamarin.tex

\chapter{Exemple}

\section{Sintaxă și elemente specifice}

Preluăm din articolul \cite{DBLP:journals/siglog/BasinCDS17} cîteva
elemente introductive privitoare la funcționarea Tamarin.

Studiul de caz prezentat este acela al schimbului de chei Diffie-Hellman (DH).

\begin{remark}\label{rk:sintaxa}
  Sintaxa folosită în cele de mai jos este imprecisă, deoarece am
  preferat lizibilitatea și claritatea. Sintaxa exactă este dată în
  fișierele-sursă conținute în directorul proiectului, în interiorul
  directorului \texttt{src}.
\end{remark}

Sursa de cod va fi un fișier cu extensia \texttt{spthy} (abreviere de la
\emph{Security Protocol THeorY}), care reprezintă
o teorie pe care compilatorul o va testa. Fișierul de teorie conține
următoarele elemente:

\textbf{Date de intrare: Teoria ecuațională:} Specificăm mesajele pentru
protocol sub forma:

\begin{verbatim}
builtins: diffie-hellman
functions: mac/2, g/0, shk/0 [private]
\end{verbatim}

Astfel, elementele specifice protocolului DH (care este inclus în biblioteca
standard, lucru menționat prin sintaxa \texttt{builtins}) sînt încărcate.
În particular, avem definit operatorul \texttt{\^}, folosit pentru exponențiere.

Se mai definește o funcție de 2 variabile \texttt{mac}, care modelează un
cod de autentificare a mesajelor (eng.\ \emph{Message Authentication Code})
și două constante, \texttt{g} pentru a modela generatorul grupului în care
are loc problema, respectiv \texttt{shk} pentru o cheie secretă partajată
(eng.\ \emph{shared secret key}). Acestea sînt declarate \texttt{private},
deci nu pot fi obținute direct de către adversar.

De asemenea, cum Tamarin este implementat în Haskell, avem suport inclus
pentru func\-ți\-i\-le specifice perechilor, \texttt{<\_,\_>, fst, snd}.

\textbf{Date de intrare: Protocolul:} Pentru modelarea protocolului vom
utiliza 3 reguli de rescriere într-un sistem bazat pe un multiset.
Fiecare regulă este un triplet, alcătuit din șiruri de fapte în membrul
stîng, etichete și membrul drept. Faptele sînt de forma $ F(t_1, \dots, t_k) $,
pentru un simbol specific $ F $ și termenii $ t_i $. Regulile protocolului
folosesc simboluri unare de fapte \texttt{Fr} și \texttt{In} pentru a
obține constante noi (eng.\ \emph{fresh names}) și mesajele de pe
rețea.

Un mesaj este trimis pe rețea folosind un simbol unar de fapte \texttt{Out}
în membrul drept.

Prima regulă creează un fir de execuție pentru protocol \texttt{tid}
care alege un exponent nou \texttt{x} și trimite participanților \texttt{g\^{}x},
concatenat cu \texttt{mac} aplicat acestei valori:

\begin{verbatim}
rule Step1: [ Fr(tid:fresh), Fr(x:fresh) ] -[ ]->
            [ Out(<g^(x:fresh), mac(shk, <g^(x:fresh), A:pub, B:pub>)>)
            , Step1(tid:fresh, A:pub, B:pub, x:fresh) ]
\end{verbatim}

În aceste linii de sintaxă se specifică explicit ca variabilele utilizate
să fie instanțiate doar cu sorturile \texttt{fresh}, respectiv \texttt{pub},
acolo unde este cazul.

Spunem că o instanță a regulii \texttt{Step1} \emph{consumă} două fapte
\texttt{Fr} pentru a obține constantele noi \texttt{tid} și \texttt{x}
și \emph{generează} un fapt \texttt{Out} care conține mesajul trimis
și un fapt \texttt{Step1} care arată că firul de execuție a finalizat
primul pas cu parametrii dați.

Remarcăm că regula nu are etichete, deci ea nu va afecta urma sistemului
de rescriere.

A doua regulă este pasul al doilea din protocol:

\begin{verbatim}
rule Step2: [ Step1(tid, A, B, x:fresh), In(<Y, mac(Y, B, A>)>) ]
              -[ Accept(tid, Y^(x:fresh)) ]-> []
\end{verbatim}

Aici se folosește faptul din pasul 1 și se verifică MAC-ul.
Eticheta din final, \texttt{Accept(tid, Y\^{}(x:fresh))}, arată
că firul de execuție \texttt{tid} a acceptat cheia de sesiune
\texttt{Y\^{}(x:fresh)}.

Regula a treia arată cheia secretă adversarului:
\begin{verbatim}
rule RevealKey: [] -[ Reveal() ]-> [ Out(shk) ]
\end{verbatim}

Eticheta \texttt{Reveal()} asigură că se va vedea pe urma sistemului
dacă și cînd a avut loc dezvăluirea.

\textbf{Date de intrare: Proprietăți:} În continuare, trebuie
specificate proprietățile pe care să le satisfacă sistemul, sub
forma unor leme. Acestea vor fi verificate și admise sau respinse
de către Tamarin. Un exemplu este:

\begin{verbatim}
lemma Accept_Secret:
    ∀ i j tid key. Accept(tid,key)@i & K(key)@j =>
        ∃ n. Reveal()@n & n < i
\end{verbatim}

Această lemă cuantifică peste momentele de timp \texttt{i, j, n} și mesajele
\texttt{tid, key}. Predicatele folosite sînt de forma \texttt{F \@ i}, care
arată că faptul \texttt{F} are loc la momentul \texttt{i}. În total, lema
arată că dacă un fir de execuție \texttt{tid} a acceptat o cheie \texttt{key}
la momentul \texttt{i}, iar cheia este cunoscută și adversarului, atunci
trebuie să existe un moment de timp \texttt{n} anterior lui \texttt{i} cînd
a avut loc dezvăluirea (\texttt{Reveal()}).

\textbf{Date de ieșire} În fine, dacă rulăm Tamarin pe informațiile
de mai sus, salvate în fișierul-sursă \texttt{example.spthy}, invocat în
linia de comandă cu:
\begin{verbatim}
$ tamarin-prover example.spthy
\end{verbatim}
rezultă următorul mesaj:

\begin{verbatim}
analyzed example.spthy: Accept_Secret (all-traces) verified (9 steps)
\end{verbatim}

Acest lucru arată că Tamarin a analizat cu succes că toate urmele protocolului
satisfac proprietatea din lema \texttt{Accept\_Secret}.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% 
\section{\texttt{FirstExample.spthy}}

Preluăm acum prezentarea unui exemplu ceva mai sofisticat, din
manualul oficial Tamarin (\cite{taman}). O notație generică
pe care o vom folosi pentru schimbul de mesaje este de felul
următor:

\begin{verbatim}
C -> S: aenc(k, pkS)
C <- S: h(k)
\end{verbatim}

Această notație arată că în protocolul considerat, clientul
\texttt{C} generează o cheie simetrică nouă (\emph{fresh}) \texttt{k},
o criptează cu o cheie publică \texttt{pkS} a unui server \texttt{S}
(notația \texttt{aenc} înseamnă \emph{criptare asimetrică}) și o
trimite serverului \texttt{S}. Serverul confirmă primirea cheii și
trimite un hash al acesteia înapoi la client.

Modelul adversarului este de tip Dolev-Yao, adică este omniprezent
în rețea și poate șterge, modifica și intercepta mesaje, fiind limitat
doar de criptografia folosită.

În continuare, detaliem sursa care se distribuie cu manualul Tamarin,
în fișierul \texttt{FirstExample.spthy}.

După declararea teoriei ce se va defini, se începe codul propriu-zis.
Astfel, primele linii de sintaxă sînt, în acest caz:

\begin{verbatim}
theory FirstExample
begin
\end{verbatim}

Apoi se declară primitivele (variabilele, funcțiile și orice alte entități
prezente deja în biblioteca standard) ce se vor folosi, iar la final,
proprietățile ce vrem să fie respectate, adică lemele.

Obiectele folosite din biblioteca standard se declară astfel:

\begin{verbatim}
builtins: hashing, asymmetric-encryption
\end{verbatim}

Aceste variabile aduc cu sine următoarele funcții utile:
\begin{itemize}
\item o funcție unară \texttt{h}, care este funcție de hashing;
\item o funcție binară \texttt{aenc}, pentru criptare asimetrică;
\item o funcție binară \texttt{adec}, pentru decriptare asimetrică;
\item o funcție unară \texttt{pk}, care reprezintă cheia publică, asociată
  unei chei private.
\end{itemize}

Totodată, modulele respective conțin și reguli de simplificare potrivite,
adică o teorie ecuațională care conține, de exemplu, ecuația care arată
că funcțiile de criptare și decriptare sînt inverse una celeilalte:
\[
  \texttt{adec(aenc(m, pk(sk)), sk) = m}.
\]

În continuare, avem de specificat regulile de funcționare. Protocolul și mediul
în care acesta se desfășoară sînt modelate folosind reguli de rescriere pe
multiseturi. Stările sistemului sînt multiseturi (eng. \qq{\emph{bags}}) de
fapte. Aceste fapte sînt predicate care arată informațiile relevante privitoare
la starea sistemului. De exemplu, un fapt de forma \texttt{Out(h(k))} arată
că protocolul a trimis mesajul \texttt{h(k)} pe un canal public, iar faptul
\texttt{In(x)} arată că protocolul a primit mesajul \texttt{x} pe canalul
public.\footnotemark

\footnotetext{Implicit, Tamarin folosește doar canale de comunicație publice,
  dar se pot defini de către utilizator și canale private. În acest exemplu
  simplu, însă, vom presupune că folosim doar canale publice.}

Prima regulă arată cum se înregistrează o cheie publică:

\begin{verbatim}
rule Register_pk:
    [ Fr(~ltk) ] --> [ ~Ltk($A, ~ltk), ~Pk($A, pk(~ltk)) ]
\end{verbatim}    

Singura premisă este să existe un fapt de tip \texttt{Fr}, care în sintaxa
standard înseamnă că este vorba despre o variabilă nouă (\emph{fresh}).
De asemenea, se folosesc următoarele prefixe în sintaxa standard, pentru
diversele tipuri de variabile:
\begin{itemize}
\item \texttt{~x} înseamnă variabilă nouă, scrisă pe larg \texttt{x:fresh};
\item \texttt{\$x} notează o variabilă publică, \texttt{x:pub};
\item \texttt{\#i} notează o variabilă temporală (un moment de timp din evoluția
  stărilor sistemului), adică \texttt{i:temporal};
\item \texttt{m} simplu notează un mesaj, adică \texttt{m:msg}.
\end{itemize}

Vom mai folosi, ca o convenție, numele \texttt{c} pentru o constantă publică,
globală. Practic, avem sorturile \texttt{msg, fresh, pub, temporal},
cu proprietatea că $ \texttt{fresh, pub} \subseteq \texttt{msg} $, iar elementele
sortului \texttt{temporal} sînt incomparabile cu celelalte.

Regula definită mai sus arată că: se generează un nume nou \texttt{~ltk}, deci
de sort \texttt{fresh}, care este noua cheie privată. Se alege (în mod nedeterminist)
un nume public \texttt{A} pentru agentul căruia i se generează perechea de chei.

Faptul care urmează, \texttt{!Ltk(\$A, ~ltk)} arată că se definește o asociere
între agentul \texttt{A} și cheia sa privată \texttt{~ltk}, iar semnul
de exclamare din fața faptului arată că acesta se poate genera de oricîte
ori este nevoie (este \emph{persistent}).

Propagarea cheii publice în rețea se face cu regula următoare:

\begin{verbatim}
rule Get_pk:
    [ !Pk(A, pubkey) ] --> [ Out(pubkey) ]
\end{verbatim}

Compromiterea cheii, adică descoperirea ei de către adversar, este formalizată
în regula următoare, unde apare și un \emph{fapt în acțiune} (eng.\
\emph{actionfact}), care nu este prezent în stările sistemului, ci doar pe
urmele sale:

\begin{verbatim}
rule Reveal_ltk:
    [ !Ltk(A, ltk) ] --[ LtkReveal(A) ]-> [ Out(ltk) ]
\end{verbatim}

Funcționarea propriu-zisă a protocolului este acum modelată de următoarele
trei reguli:

\begin{verbatim}
// Începe un nou fir de execuție pentru client,
// prin alegerea unui server în mod non-determinist.
rule Client_1:
    [ Fr(~k), !Pk($S, pkS)] -->
    [ Client_1 ($S, ~k), Out( aenc(~k, pkS) ) ]
\end{verbatim}

Se generează o cheie nouă (\texttt{Fr(~k)}) și se caută cheia publică
pentru sever, iar apoi se stochează serverul și cheia pentru pasul următor
și se trimite cheia de sesiune criptată către server.

\begin{verbatim}
rule Client_2:
    [ Client_1(S, k), In( h(k) ) ] --[ SessKeyC( S, k ) ]-> []
\end{verbatim}

În acest pas, se apelează la sesiunea anterioară pentru server și cheie,
se primește cheia căreia i s-a aplicat funcția hash și se menționează
(pe urmă, deci ca fapt în acțiune) că s-a stabilit o cheie de sesiune \texttt{k}
cu serverul \texttt{S}.

\begin{verbatim}
// Răspunsul unui server într-un fir de execuție la cererea
// din partea unui client.
rule Serv_1:
    [ !Ltk($S, ~ltkS), In( request ) ]
    --[ AnswerRequest($S, adec(request, ~ltkS)) ]->
    [ Out( h(adec(request, ~ltkS)) ) ]
\end{verbatim}

Practic, se caută cheia privată după primirea unei cereri, se înregistrează
(ca fapt în acțiune) răspunsul la această cerere și se returnează hash-ul
cererii decriptate.

Regulile pe care trebuie să le satisfacă protocolul sînt descrise prin
următoarele leme.

\begin{verbatim}
lemma Client_session_key_secrecy:
  " not( /* nu este cazul ca */
        Ex S k #i #j.
          /* clientul a stabilit o cheie de sesiune k cu serverul S */
          SessKeyC(S, k) @ #i
          /* iar adversarul să cunoască cheie */
          & K(k) @ #j
          /* fără a fi aplicat o dezvăluire pe S */
          & not(Ex #r. LtkReveal(S) @ r)
        )
  "

lemma Client_auth:
  " /* Pentru toate cheile de sesiune 'k'
       stabilite de clienți cu serverul 'S' */
    ( All S k #i. SessKeyC(S, k) @ #i
        ==>
      /* există un server care a răspuns la cerere */
        ( (Ex #a. AnswerRequest(S, k) @ a)
      /* sau adversarul a aflat cheia înainte de a fi stabilită */
          | (Ex #r. LtkReveal(S) @ r & r < i)
        )
    )
  "
\end{verbatim}

Putem întări autorizarea, cerînd ca aceasta să fie injectivă, adică să
nu existe doi clienți care au avut aceeași cerere la server:

\begin{verbatim}
lemma Client_auth_injective:
  " /* Pentru toate cheile de sesiune 'k' stabilite cu serverul 'S' */
    ( All S k #i. SessKeyC(S, k) @ #i
        ==>
        /* există un server care a răspuns */
      ( (Ex #a. AnswerRequest(S, k) @ a
        /* și niciun alt client n-a avut exact aceeași cerere */
        & (All #j. SessKeyC(S, k) @ #j ==> #i = #j)
        ) /* sau adversarul a aflat cheia înainte de a fi stabilită */
        | (Ex #r. LtkReveal(S) @ r & r < i)
      )
    )
  "
\end{verbatim}

Mai putem adăuga încă o proprietate pentru a ne asigura că proprietățile
nu se execută pe un model vid:
\begin{verbatim}
lemma Client_session_key_honest_setup:
  exists-trace
    " Ex S k #i.
        SessKeyC(S, k) @ #i
        & not(Ex #r. LtkReveal(S) @ r)
    "
\end{verbatim}

Întregul exemplu este conținut în fișierul sursă din proiect.

Evaluarea fișierului se face din linia de comandă sau se poate apela la
unelte cu interfață grafică, detaliate în \cite[pp.\ 20--26]{taman}.

%%% Local Variables:
%%% mode: latex
%%% TeX-master: "../tamarin"
%%% End:
